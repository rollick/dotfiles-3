#!/bin/zsh
# Hash normal directories
() {
	hash_directory() {
		local DIR="${2}"
		local NAME="${1}"

		if [[ -d "${DIR}" ]]; then
			hash -d "${NAME}"="${DIR}"
		else
			unhash -d "${NAME}" &>|/dev/null
		fi
	}

	hash_directory "alog" "/var/log/old"
	hash_directory "cups-pdf" "/var/spool/cups-pdf/${USER}"
	hash_directory "log" "/var/log"
	hash_directory "media" "/run/media/${USER}"
	hash_directory "paccache" "/var/cache/pacman/pkg"
	hash_directory "virtualenvs" "${WORKON_HOME}"

	# Cleanup
	unfunction hash_directory
}

# Add local ABS mirror
() {
	local DIR

	if [[ -f '/etc/abs.conf' ]]; then
		DIR=$(source "/etc/abs.conf"; print "${ABSROOT/%\//}")
		if [[ -d "${DIR}" ]]; then
			hash -d abs="${DIR}"
		fi
	else
		unhash -d "abs" &>|/dev/null
	fi
}

# Hash git repositories
() {
	local _XDG_DATA_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}"
	local _XDG_DOCUMENTS_DIR="${XDG_DOCUMENTS_DIR:-${HOME}/Documents}"
	local name
	local subdir

	hash_git_repos() {
		local BASENAME="${1}"
		local ROOT="${2}"
		local name
		local subdir

		for subdir in "${ROOT}"/*(FN); do
			if [[ -d "${subdir}/.git" ]]; then
				name="${subdir:t}"
				hash -d "git-${BASENAME}-${name}"="${subdir}"
			fi
		done
	}

	# Normalize paths
	_XDG_DATA_HOME="$(readlink -m ${_XDG_DATA_HOME})"
	_XDG_DOCUMENTS_DIR="$(readlink -m ${_XDG_DOCUMENTS_DIR})"

	# Remove all hashed "regular" git directories
	unhash -dm "git-root*" &>|/dev/null
	unhash -dm "git-local*" &>|/dev/null
	unhash -dm "git-remote*" &>|/dev/null

	# Hash "regular" git repositories
	if [[ -d "${_XDG_DOCUMENTS_DIR}/Code" ]]; then
		hash -d git-root="${_XDG_DOCUMENTS_DIR}/Code"

		if [[ -d "${_XDG_DOCUMENTS_DIR}/Code/Local" ]]; then
			hash -d git-local="${_XDG_DOCUMENTS_DIR}/Code/Local"
			hash_git_repos "local" "${_XDG_DOCUMENTS_DIR}/Code/Local"
		fi

		if [[ -d "${_XDG_DOCUMENTS_DIR}/Code/Remote" ]]; then
			hash -d git-remote="${_XDG_DOCUMENTS_DIR}/Code/Remote"
			hash_git_repos "remote" "${_XDG_DOCUMENTS_DIR}/Code/Remote"
		fi
	fi

	# Cleanup
	unfunction hash_git_repos
}
