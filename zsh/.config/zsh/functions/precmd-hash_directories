#!/bin/zsh
# Hash normal directories
() {
	local XDG_DOCUMENTS_DIR="${XDG_DOCUMENTS_DIR:-${HOME}/Documents}"

	hash_directory() {
		local DIR="${2}"
		local NAME="${1}"

		if [[ -d "${DIR}" ]]; then
			hash -d "${NAME}"="${DIR}"
		else
			unhash -d "${NAME}" &>|/dev/null
		fi
	}

	hash_directory "alog" "/var/log/old"
	hash_directory "cups-pdf" "/var/spool/cups-pdf/${USER}"
	hash_directory "git-local" "${XDG_DOCUMENTS_DIR}/Code/Local"
	hash_directory "git-remote" "${XDG_DOCUMENTS_DIR}/Code/Remote"
	hash_directory "git-root" "${XDG_DOCUMENTS_DIR}/Code"
	hash_directory "log" "/var/log"
	hash_directory "media" "/run/media/${USER}"
	hash_directory "paccache" "/var/cache/pacman/pkg"
	hash_directory "virtualenvs" "${WORKON_HOME}"

	# Cleanup
	unfunction hash_directory
}

# Add local ABS mirror
() {
	local absroot

	get_absroot() {
		local absroot

		if [[ -n ${ABSROOT} ]]; then
			print "${ABSROOT}"
		else
			for f in "${@}"; do
				if [[ -f "${f}" ]]; then
					absroot=$(source "${f}"; print "${ABSROOT/%\//}")
					break
				fi
			done

			print "${absroot}"
		fi
	}

	absroot=$(get_absroot "${HOME}/.abs.conf" '/etc/abs.conf')

	# Hash directory
	if [[ -n $absroot ]]; then
		if [[ -d "${absroot}" ]]; then
			hash -d abs="${absroot}"
		fi
	else
		unhash -d "abs" &>|/dev/null
	fi

	# Cleanup
	unfunction get_absroot
}
