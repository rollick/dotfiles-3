function prompt_default_setup() {
	# Options for rsc-default prompt
	##################
	prompt_opts=(cr subst percent)

	# Initialize precmd hooks
	##################
	add-zsh-hook precmd prompt_default_prompt

	# Information about this prompt
	##################
	export PROMPT_LINES=2
}

function prompt_default_prompt() {
	# Set local variables
	##################
	local count
	local ref
	local stash

	local prompt_char_tlc
	local prompt_char_blc
	local prompt_char_close
	local prompt_char_mlc
	local prompt_char_hyphen
	local prompt_color_div
	local prompt_color_dir
	local prompt_color_line
	local prompt_color_rprompt
	local prompt_color_user

	local prompt_gfx_blc
	local prompt_gfx_close
	local prompt_gfx_colon
	local prompt_gfx_hyphen
	local prompt_gfx_lbracket
	local prompt_gfx_lparent
	local prompt_gfx_mlc
	local prompt_gfx_plus
	local prompt_gfx_rbracket
	local prompt_gfx_rparent
	local prompt_gfx_separator
	local prompt_gfx_tlc

	local prompt_info_git=""
	local prompt_info_jobs=""
	local prompt_info_tmux=""

	# Set color information
	##################
	if (( ${UID} == 0 )); then
		local prompt_color_user='%{$fg_bold[red]%}'
	else
		local prompt_color_user='%{$fg_bold[green]%}'
	fi
	prompt_color_dir='%{$fg_bold[blue]%}'
	prompt_color_div='%{$fg_bold[black]%}'
	prompt_color_line='%{$fg_bold[cyan]%}'
	prompt_color_rprompt="${prompt_color_dir}"

	# Use some ANSI characters if supported
	##################
	prompt_char_tlc=$'\u250C'
	prompt_char_blc=$'\u2514'
	prompt_char_close=$'\u25B6'
	prompt_char_mlc=$'\u251C'
	prompt_char_hyphen=$'\u2500'

	# Combine colors and characters
	##################
	prompt_gfx_blc="${prompt_color_line}${prompt_char_blc}"
	prompt_gfx_close="${prompt_color_line}${prompt_char_close}"
	prompt_gfx_colon="${prompt_color_div}:"
	prompt_gfx_hyphen="${prompt_color_line}${prompt_char_hyphen}"
	prompt_gfx_lbracket="${prompt_color_div}["
	prompt_gfx_lparent="${prompt_color_div}("
	prompt_gfx_mlc="${prompt_color_div}${prompt_char_mlc}"
	prompt_gfx_plus="${prompt_color_line}+"
	prompt_gfx_rbracket="${prompt_color_div}]"
	prompt_gfx_rparent="${prompt_color_div})"
	prompt_gfx_separator="${prompt_color_div}|"
	prompt_gfx_tlc="${prompt_color_line}${prompt_char_tlc}"

	# tmux information for RPROMPT
	####################
	if which tmux &>|/dev/null; then
		if [[ -z "${TMUX_PANE}" ]] && tmux list-session &>|/dev/null; then
			tmux_sessions="$(tmux list-sessions |wc -l)"

			if tmux list-sessions |grep -q '^cmus: '; then
				tmux_sessions=$(( tmux_sessions - 1 ))
			fi

			if [[ ${tmux_sessions} -gt 0 ]]; then
				prompt_info_tmux=$'\u2B12 '

				prompt_info_tmux+="${tmux_sessions}"
			fi
		fi
		unset tmux_sessions
	fi

	# job information for RPROMPT
	####################
	count="$(jobs -l |wc -l)"

	if [[ ${count} -ne 0 ]]; then
		prompt_info_jobs="Jobs: ${count}"
	fi

	# git information for RPROMPT
	####################
	if which git &>|/dev/null; then
		ref=${$(git symbolic-ref HEAD 2>|/dev/null)#refs/heads/}
		if [[ -n "${ref}" ]]; then
			prompt_info_git=$'\u00B1 '

			# Set branch
			prompt_info_git+="${ref}"

			# Set status icon
			prompt_info_git+=" "
			if [[ -n $(git status -s 2>|/dev/null) ]]; then
				# Ballot X
				prompt_info_git+=$'\u2717'
			else
				# Checkmark
				prompt_info_git+=$'\u2713'
			fi

			# Stash
			stash=$(git stash list 2>|/dev/null |wc -l)
			if (( ${stash} > 0 )); then
				# Eight spoked asterisk
				prompt_info_git+=$' \u2733'
			fi
		fi
	fi

	# Set terminal title
	####################
	case "${TERM}" in
		xterm* | rxvt* )	print -Pn "\e]0;%n@%m: %~\a";;
		screen*)			print -Pn "\ek%n@%m: %~\e\\";;
	esac

	# Main prompt
	####################
	# Start prompt
	PROMPT="${prompt_gfx_tlc}"

	# user and hostname information
	PROMPT+="${prompt_gfx_lparent}${prompt_color_user}%n@%m${prompt_gfx_rparent}"
	PROMPT+="${prompt_gfx_hyphen}"

	# PWD information
	PROMPT+="${prompt_gfx_lparent}${prompt_color_dir}%~${prompt_gfx_rparent}"

	# Close prompt
	PROMPT+="${prompt_newline}"
	PROMPT+="${prompt_gfx_blc}"
	PROMPT+="${prompt_gfx_close}%{$reset_color%} "

	# Secondary prompt: More information needed
	####################
	PROMPT2="${prompt_gfx_mlc}${prompt_gfx_close}%{$reset_color%} "

	# Execution trace prompt
	####################
	PROMPT4="${prompt_gfx_plus}%{$reset_color%} "

	# RPROMPT
	####################
	() {
		unset RPROMPT
		until [[ -z "${1}" ]]; do
			if [[ "${RPROMPT}" != "" ]]; then
				RPROMPT+="${prompt_gfx_separator}"
			else
				RPROMPT="${prompt_gfx_lbracket}"
			fi
			RPROMPT+="${prompt_color_rprompt}${1}"
			shift
		done
		if [[ -n "${RPROMPT}" ]]; then
			RPROMPT+="${prompt_gfx_rbracket}%{$reset_color%}"
		fi
	} ${prompt_info_git} ${prompt_info_tmux} ${prompt_info_jobs}

	# Prompt for spelling correction
	####################
	SPROMPT="zsh: correct %R to %r ([Y]es/[N]o/[E]dit/[A]bort) "
}

prompt_default_setup
