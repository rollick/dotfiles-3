#!/bin/zsh
# Hash normal directories and automatically remove obsolete hashes
() {
	local XDG_DOCUMENTS_DIR="${XDG_DOCUMENTS_DIR:-${HOME}/Documents}"

	hash_directory() {
		local dir
		local NAME

		NAME="${1}"
		shift

		unhash -d "${NAME}" &>|/dev/null

		for dir in $@; do
			if [[ -d "${dir}" ]]; then
				hash -d "${NAME}"="${dir}"
				break
			fi
		done
	}

	hash_directory "alog" "/var/log/old"
	hash_directory "cups-pdf" "/var/spool/cups-pdf/${USER}"
	hash_directory "git-local" "${XDG_DOCUMENTS_DIR}/Code/Local"
	hash_directory "git-remote" "${XDG_DOCUMENTS_DIR}/Code/Remote"
	hash_directory "git-root" "${XDG_DOCUMENTS_DIR}/Code"
	hash_directory "log" "/var/log"
	hash_directory "media" "/run/media/${USER}" "/media/${USER}"
	hash_directory "paccache" "/var/cache/pacman/pkg"
	hash_directory "run" "${XDG_RUNTIME_DIR}"
	hash_directory "virtualenvs" "${WORKON_HOME}"

	# Cleanup
	unfunction hash_directory
}
