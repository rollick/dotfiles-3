#!/bin/zsh
# Setup $LS_COLORS
####################
if which dircolors &>|/dev/null; then
	local CACHEDIR="${XDG_CACHE_HOME:-${HOME}/.cache}"
	local CACHEFILE="${CACHEDIR}/dircolors"
	[[ ! -d "${CACHEDIR}" ]] && mkdir -p "${CACHEDIR}"
	[[ ! -f "${CACHEFILE}" ]] && touch "${CACHEFILE}"

	if [[ "$(dircolors --version)" != $(<"${CACHEFILE}") ]]; then
		dircolors --version >| "${CACHEFILE}"

		# Don't overwrite user settings
		# Since most users don't touch their dircolors configuration,
		# just copy their old one to ${HOME}/.dircolors.old and inform them about
		# it.
		if [[ -f "${HOME}/.dircolors" ]]; then
			mv -f "${HOME}/.dircolors" "${HOME}/.dircolors.old"

			[[ ! -f "${HOME}/.dircolors" ]] && dircolors -p >| "${HOME}/.dircolors"

			if [[ $(<"${HOME}/.dircolors") != $(<"${HOME}/.dircolors.old") ]]; then
				printmsg "Old dircolors configuration moved to ${HOME}/.dircolors.old"
			else
				rm "${HOME}/.dircolors.old"
			fi
		fi
	fi

	# Create missing ~/.dircolors
	[[ ! -f "${HOME}/.dircolors" ]] && dircolors -p >| "${HOME}/.dircolors"

	# Load dircolors settings
	if [[ "${TERM}" == *-256color && -f "${HOME}/.dircolors-sd" ]]; then
		eval $(dircolors -b "${HOME}/.dircolors-sd")
	else
		eval $(dircolors -b "${HOME}/.dircolors")
	fi
fi
